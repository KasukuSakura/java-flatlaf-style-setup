plugins {
    id 'java'
    id 'java-library'
    id 'io.github.karlatemp.publication-sign' version '1.3.40'
    id 'maven-publish'
}

allprojects {
    group 'com.kasukusakura'
    version '0.0.1'

    repositories {
        mavenCentral()
    }
}
description 'A tool for setup flatlaf'


dependencies {
    def flatlafVer = '2.6'

    compileOnly "com.formdev:flatlaf:$flatlafVer"
    compileOnly "com.formdev:flatlaf-extras:$flatlafVer"
    compileOnly "com.formdev:flatlaf-intellij-themes:$flatlafVer"

    testImplementation "com.formdev:flatlaf:$flatlafVer"
    testImplementation "com.formdev:flatlaf-extras:$flatlafVer"
    testImplementation "com.formdev:flatlaf-intellij-themes:$flatlafVer"
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task sourcesJar(type: Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

task stubJavadoc(type: Jar) {
    archiveBaseName.set 'base-javadoc'
    archiveClassifier.set 'javadoc'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version

            from components.java
            artifact rootProject.stubJavadoc
            artifact sourcesJar

            pom { pom ->
                pom.name = rootProject.name
                pom.description = rootProject.description
            }
        }
    }
}